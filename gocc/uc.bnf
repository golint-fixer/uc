// A grammar for the µC programming language [1].
//
// The content and structure of this document is heavily influenced by the Go
// Programming Language Specification [2] and some parts are therefore governed
// by a BSD-style license [3]. Any original content of this document is hereby
// released into the public domain [4].
//
// References:
//    [1]: https://www.it.uu.se/katalog/aleji304/CompilersProject/uc.html
//    [2]: http://golang.org/ref/spec
//    [3]: http://golang.org/LICENSE
//    [4]: https://creativecommons.org/publicdomain/zero/1.0/

// # Source code representation
//

// ## Characters
//

// An arbitrary ASCII character except null (0x00), new line (0x0A), carriage
// return (0x0D), apostrophe (0x27), double quote (0x22) and backslash (0x5C).
_ascii_char
	: '\x01' - '\x09'
	| '\x0B' - '\x0C'
	| '\x0E' - '\x21'
	| '\x23' - '\x26'
	| '\x28' - '\x5B'
	| '\x5D' - '\x7F'
;

_ascii_letter : 'a' - 'z' | 'A' - 'Z' ;
_ascii_digit  : '0' - '9' ;

// ## Letters and digits
//

_letter        : _ascii_letter | '_' ;
_decimal_digit : _ascii_digit ;
_decimals      : _decimal_digit { _decimal_digit } ;

// # Lexical elements
//

// ## Comments
//

_line_comment
	: '/' '/' { . } '\n'
;

_block_comment : '/' '*' { . | '*' } '*' '/' ;
!comment       : _line_comment | _block_comment ;

// ## Tokens
//

// White space, formed from spaces (0x20), horizontal tabs (0x09), new line
// (line-feed (0x0A) or carriage-return (0x0D)), vertical tabs (0x0B), and form-
// feeds (0x0C) (§6.4), is ignored except as it separates tokens that would
// otherwise combine into a single token.
!whitespace : ' ' | '\t' | '\v' | '\f' | '\r' | '\n' ;

// ## Identifiers
//

ident : _letter { _letter | _decimal_digit } ;

// ## Integer literals
//

int_lit : _decimals | _char_lit ;

// ## Character literals
//

_escaped_char : '\\' 'n' ;
_char_lit     : '\'' ( _ascii_char | '"' | _escaped_char ) '\'' ;

// # Syntaxic production rules
//

<< import (
	"strconv"

	"github.com/mewmew/uc/ast"
	"github.com/mewmew/uc/ast/astx"
	gocctoken "github.com/mewmew/uc/gocc/token"
	"github.com/mewmew/uc/token"
	"github.com/mewmew/uc/types"
) >>

Program
	: TopLevelDeclList
;

TopLevelDeclList
	: empty
	| TopLevelDecl TopLevelDeclList
;

TopLevelDecl
	: VarDecl ";"
	// TypeName : "char" | "int" | "void" ;
	| TypeName ident "(" Params ")" FuncBody
;

VarDecl
	: ScalarDecl
	| ArrayDecl
;

ScalarDecl
	// TypeName : "char" | "int" ;
	: TypeName ident
;

ArrayDecl
	// TypeName : "char" | "int" ;
	: TypeName ident "[" int_lit "]"    << astx.NewArrayDecl($0, $1, $3) >>
;

// TODO: Remove the Locals production rule and replace the TypeName rule with
// TypeName : ident ;, thus making use of The Lexer Hack as implemented by
// Clang.
//
// Discussion taking place in issue #27, https://github.com/mewmew/uc/issues/27

TypeName
	// TypeName : "char" | "int" | "void" ;
	: "char"                            << astx.NewBasicType($0) >>
	| "int"                             << astx.NewBasicType($0) >>
	| "void"                            << astx.NewBasicType($0) >>
;

FuncBody
	: "{" Locals Stmts "}"
	| ";"
;

Params
	// TypeName : "void" ;
	: TypeName
	| FormalList
;

FormalList
	: FormalDecl
	| FormalDecl "," FormalList
;

FormalDecl
	: ScalarDecl
	// TypeName : "char" | "int" ;
	| TypeName ident "[" "]"
;

Locals
	: empty
	| VarDecl ";" Locals
;

Stmt
	: Expr ";"                          << astx.NewExprStmt($0) >>
	| "return" Expr ";"                 << astx.NewReturnStmt($1) >>
	| "return" ";"                      << &ast.ReturnStmt{}, nil >>
	| "while" Condition Stmt            << astx.NewWhileStmt($1, $2) >>
	| "if" Condition Stmt ElsePart      << astx.NewIfStmt($1, $2, $3) >>
	| "{" Stmts "}"                     << astx.NewBlockStmt($1) >>
	| ";"                               << &ast.EmptyStmt{}, nil >>
;

ElsePart
	: empty                             << nil, nil >>
	| "else" Stmt                       << $1, nil >>
;

Condition
	: ParenExpr
;

Stmts
	: empty
	| StmtList
;

StmtList
	: Stmt                              << astx.NewStmtList($0) >>
	| StmtList Stmt                     << astx.AppendStmt($0, $1) >>
;

Expr
	: Expr2R
;

// TODO: Figure out how to represent left- and right-associativity. Perhaps this
// may be done during the generation of the AST?

// Right-associative binary expressions with precedence 2.
//
//    2R: =
Expr2R
	: Expr5L
	| Expr2R "=" Expr5L                 << astx.NewBinaryExpr($0, token.Assign, $2) >>
;

// Left-associative binary expressions with precedence 5.
//
//    5L: &&
Expr5L
	: Expr9L
	| Expr5L "&&" Expr9L                << astx.NewBinaryExpr($0, token.Land, $2) >>
;

// Left-associative binary expressions with precedence 9.
//
//    9L: == !=
Expr9L
	: Expr10L
	| Expr9L "==" Expr10L               << astx.NewBinaryExpr($0, token.Eq, $2) >>
	| Expr9L "!=" Expr10L               << astx.NewBinaryExpr($0, token.Ne, $2) >>
;

// Left-associative binary expressions with precedence 10.
//
//    10L: < > <= >=
Expr10L
	: Expr12L
	| Expr10L "<" Expr12L               << astx.NewBinaryExpr($0, token.Lt, $2) >>
	| Expr10L ">" Expr12L               << astx.NewBinaryExpr($0, token.Gt, $2) >>
	| Expr10L "<=" Expr12L              << astx.NewBinaryExpr($0, token.Le, $2) >>
	| Expr10L ">=" Expr12L              << astx.NewBinaryExpr($0, token.Ge, $2) >>
;

// Left-associative binary expressions with precedence 12.
//
//    12L: + -
Expr12L
	: Expr13L
	| Expr12L "+" Expr13L               << astx.NewBinaryExpr($0, token.Add, $2) >>
	| Expr12L "-" Expr13L               << astx.NewBinaryExpr($0, token.Sub, $2) >>
;

// Left-associative binary expressions with precedence 13.
//
//    13L: * /
Expr13L
	: Expr14
	| Expr13L "*" Expr14                << astx.NewBinaryExpr($0, token.Mul, $2) >>
	| Expr13L "/" Expr14                << astx.NewBinaryExpr($0, token.Div, $2) >>
;

// Unary expressions with precedence 14.
//
//    14: - !
Expr14
	: Expr15
	| "-" Expr15                        << astx.NewUnaryExpr(token.Sub, $1) >>
	| "!" Expr15                        << astx.NewUnaryExpr(token.Not, $1) >>
;

// TODO: Check that integer litearls, identifiers, index expressions, call
// expressions, and parentesized espressions have precedence 15.

// TODO: Handle char_lit explicitly?

// Expressions with precedence 15.
Expr15
	: int_lit                           << astx.NewBasicLit($0, token.IntLit) >>
	| ident                             << astx.NewIdent($0) >>
	| ident "[" Expr "]"                << astx.NewIndexExpr($0, $2) >>
	| ident "(" Args ")"                << astx.NewCallExpr($0, $2) >>
	| ParenExpr
;

ParenExpr
	: "(" Expr ")"                      << astx.NewParenExpr($1) >>
;

Args
	: empty                             << nil, nil >>
	| ExprList
;

ExprList
	: Expr                              << astx.NewExprList($0) >>
	| ExprList "," Expr                 << astx.AppendExpr($0, $2) >>
;
